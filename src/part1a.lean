import tactic
import analysis.special_functions.log
import analysis.special_functions.log_deriv
import data.fintype.basic
import algebra.big_operators.basic
import algebra.big_operators.intervals
import data.finset.sum
import data.real.basic
import topology.instances.real
import topology.instances.ennreal
import order.filter
import order.bounded_order
import analysis.special_functions.pow

import part0

open_locale filter topological_space classical
open_locale big_operators -- notation ‚àë for finite sums

open_locale classical real topological_space nnreal ennreal filter big_operators

open real
open finset
open filter
open nat


-- part 1 of https://proofwiki.org/wiki/Stirling%27s_Formula
-- first section of part 1

lemma tendsto_succ (an : ‚Ñï ‚Üí ‚Ñù) (a:‚Ñù): tendsto an at_top (ùìù a) ‚Üî
tendsto (Œª n : ‚Ñï, (an n.succ)) at_top (ùìù a) :=
begin
  split,
  {
    intro h,
    -- rw tendsto at h,
    rw tendsto_at_top' at h,
    rw tendsto_at_top',
    intros,
    have g := h s H,
    cases g with m gm,
    use m,
    intro b,
    intro hb,
    have hbsucc: b.succ >= m := le_succ_of_le hb,
    exact gm b.succ hbsucc,
  },
  { intro h,
    -- rw tendsto at h,
    rw tendsto_at_top' at h,
    rw tendsto_at_top',
    intros,
    have g := h s H,
    cases g with m gm,
    use m.succ,
    intro b,
    intro hb,
    cases b,
    exfalso,
    exact not_succ_le_zero m hb,
    have hbm: b >= m := succ_le_succ_iff.mp hb,
    exact gm b hbm,
  },
end

--can one do this with is_compl_even_odd or has_sum.even_add_odd?
lemma finset_sum_even_odd  {f : ‚Ñï ‚Üí ‚Ñù} (n : ‚Ñï):
‚àë k in range n, f k =
(‚àë l in (range n).filter(odd), f l) +
(‚àë m in (range n).filter(even), f m) :=
begin
  have h_union: ‚àÄ ( n : ‚Ñï), range n  =
  (range n).filter(odd) ‚à™ (range n).filter(even) :=
  begin
    intro n,
    induction n with d hd,
    simp only [range_zero, filter_true_of_mem, not_mem_empty, forall_false_left, forall_const, empty_union],
    repeat {rw [range_succ]},
    repeat {rw [filter_insert]},

    by_cases h: even d,
      rw [if_pos h, if_neg (even_iff_not_odd.mp h)],
      rw union_insert,
      exact congr_arg (insert d) hd,
    rw [if_neg h, if_pos (odd_iff_not_even.mpr h)],
    rw insert_union,
    exact congr_arg (insert d) hd,
  end,
  nth_rewrite 0 h_union,
  have h_disjoint: ‚àÄ (n : ‚Ñï), disjoint  ((range n).filter(odd))
     ((range n).filter(even)) :=
  begin
    intro n,
    induction n with d hd,
    simp only [range_zero, filter_true_of_mem, not_mem_empty, forall_false_left, forall_const, disjoint_empty_right],
    repeat {rw range_succ},
    repeat {rw filter_insert},
    by_cases h: even d,
      rw [if_pos h, if_neg (even_iff_not_odd.mp h)],
      rw disjoint_insert_right,
      split,
        rw [mem_filter, not_and],
        intro h‚ÇÇ,
        exact absurd h‚ÇÇ not_mem_range_self,
      assumption,
    rw [if_neg h, if_pos (odd_iff_not_even.mpr h)],
      rw disjoint_insert_left,
      split,
        rw [mem_filter, not_and],
        tauto,
      assumption,
  end,
  rw sum_union (h_disjoint n),
end


lemma finset_reindex_odd {f : ‚Ñï ‚Üí ‚Ñù} (n : ‚Ñï):
‚àë l in (range (2*n)).filter(odd), f l = ‚àë l in (range n), f (2*l + 1) :=
begin
  induction n with d hd,
  simp only [mul_zero, range_zero, filter_true_of_mem, not_mem_empty, forall_false_left, forall_const, sum_empty],
  rw [mul_succ, add_succ, add_succ, add_zero],
  repeat {rw range_succ},
  repeat {rewrite [finset.sum_insert]},
  repeat {rewrite [finset.filter_insert]},
  have h‚ÇÅ : ¬¨ odd ( 2* d), by
    simp only [odd_iff_not_even, even.mul_right, even_two, not_true, not_false_iff],
  have h‚ÇÇ: odd (2 * d).succ, by
    {simp only [odd_iff_not_even, h‚ÇÅ, even_succ],
    rw ‚Üêodd_iff_not_even,
    assumption},

  rw [if_neg h‚ÇÅ, if_pos h‚ÇÇ],

  repeat {rw finset.sum_insert},
  simp only [add_right_inj],
  exact hd,
  rw [mem_filter],
  suffices :(2 * d).succ ‚àâ range (2 * d),
  begin
  apply not_and.mpr,
  exact not.elim this,
    end,

  rw mem_range,
  exact not_succ_lt_self,

  rw mem_range,
  exact irrefl d,
end



lemma finset_reindex_even {f : ‚Ñï ‚Üí ‚Ñù} (n : ‚Ñï):
‚àë l in (range (2*n)).filter(even), f l = ‚àë l in (range n), f (2*l) :=
begin
  induction n with d hd,
  simp only [mul_zero, range_zero, filter_true_of_mem, not_mem_empty, forall_false_left, forall_const, sum_empty],
  rw [mul_succ, add_succ, add_succ, add_zero],
  repeat {rw range_succ},
  repeat {rewrite [finset.sum_insert]},
  repeat {rewrite [finset.filter_insert]},
  have h‚ÇÅ : even ( 2 * d), by exact even_two_mul d,
  have h‚ÇÇ : ¬¨(even ((2 * d).succ)), by  {simp only [even_succ, h‚ÇÅ], tautology},

  rw [if_neg h‚ÇÇ, if_pos h‚ÇÅ],
  rw finset.sum_insert,
  simp only [add_right_inj],
  exact hd,

  rw [mem_filter],
  suffices : (2 * d) ‚àâ range (2 * d), by tauto,
  rw mem_range,
  exact irrefl (2*d),

  rw mem_range,
  exact irrefl d,
end

noncomputable def an (n : ‚Ñï) : ‚Ñù  := (n.factorial :‚Ñù )
/ ((real.sqrt(2*(n))*((n/(exp 1)))^n))

noncomputable def term (x : ‚Ñù)(n : ‚Ñï) : ‚Ñù :=
   ((-1)*((-x)^(n + 1) / ((n : ‚Ñù) + 1)) + (x^(n + 1)/((n:‚Ñù) + 1)))

lemma term_def : ‚àÄ (x: ‚Ñù) , term x =(Œª n,  ((-1)*((-x)^(n + 1) / ((n : ‚Ñù) + 1)) + (x^(n + 1)/((n:‚Ñù) + 1)))) :=
begin
  intros,
  refl,
end


lemma log_sum_plus_minus (x : ‚Ñù) (hx: |x| < 1) : has_sum (Œª k:‚Ñï,
(2:‚Ñù)*(1/(2*‚Üëk + 1))*(x^(2* k + 1))) (log (1 + x) - log(1 - x)):=
begin
  have min_one_not_zero : (-1 : ‚Ñù) ‚â† ( 0 : ‚Ñù), by linarith,
  have h_min_one_ne_one:  (-1 : ‚Ñù) ‚â† ( 1 : ‚Ñù), by linarith,

  have h‚ÇÅ, from has_sum_pow_div_log_of_abs_lt_1 hx,
  have h‚ÇÇ', from has_sum_pow_div_log_of_abs_lt_1 (eq.trans_lt (abs_neg x) hx),
  have h‚ÇÇ, from (has_sum_mul_left_iff min_one_not_zero).mp h‚ÇÇ',
  rw [neg_one_mul, neg_neg, sub_neg_eq_add 1 x] at h‚ÇÇ,
  have h‚ÇÉ, from has_sum.add h‚ÇÇ h‚ÇÅ,
  rw [tactic.ring.add_neg_eq_sub] at h‚ÇÉ,
  rw [‚Üêterm_def x ] at h‚ÇÉ,

  let g := (Œª (n : ‚Ñï),  (2 * n)),

  rw ‚Üêfunction.injective.has_sum_iff (nat.mul_right_injective two_pos) _ at h‚ÇÉ,

  suffices h_term_eq_goal : (term x ‚àò g) = (Œª k : ‚Ñï, 2*(1 / (2 * (k : ‚Ñù) + 1)) * x^(2 * k  + 1)),
  begin
    rw h_term_eq_goal at h‚ÇÉ,
    exact h‚ÇÉ,
  end,

  apply funext,
  intro n,

  rw [function.comp_app],
  simp only [g, term],
  rw odd.neg_pow (‚ü®n, rfl‚ü© :odd (2 * n + 1)) x,
  rw [neg_one_mul, neg_div, neg_neg, cast_mul, cast_two],
  ring_nf,

  intros m hm,
  simp only [range_two_mul, set.mem_set_of_eq] at hm,
  simp only [term],
  rw [even.neg_pow (even_succ.mpr hm), succ_eq_add_one],
  ring_nf,
end


lemma aux_log (n : ‚Ñï) (hn: n ‚â† 0):
log (n.succ/n) = log (1 + 1 / (2*n + 1)) - log (1 - 1/(2*n +1)):=
begin
  have h‚ÇÅ: (2:‚Ñù)*n + 1 ‚â† 0 :=
  begin
    norm_cast,
    exact succ_ne_zero (2*n),
  end,
  have h‚ÇÇ: (2:‚Ñù)*n + 1 = (2:‚Ñù)*n + 1 := by refl,
  calc log (n.succ/n) = log(n.succ) - log(n) :
    log_div (cast_ne_zero.mpr (succ_ne_zero n)) (cast_ne_zero.mpr hn)
  ... = log(n.succ) - log(n) +  log 2 - log 2: by simp only [add_tsub_cancel_right]
  ... = log 2 + log(n.succ) - (log 2 + log n): by linarith
  ... = log 2 + log(n.succ) - log(2*n) :
    by rw log_mul (two_ne_zero) (cast_ne_zero.mpr hn)
  ... = log(2 * n.succ) - log(2*n) :
    by rw log_mul (two_ne_zero) (cast_ne_zero.mpr (succ_ne_zero n))
  ... = log(2*n.succ) - log(2*n) - log (2*n + 1) + log (2*n + 1) : by simp only [sub_add_cancel]
  ... = log(2*n.succ) - log (2*n + 1) - (log (2*n) - log (2*n + 1)) : by linarith
  ... = log ((2*n.succ)/(2*n + 1))  - (log (2*n) - log (2*n + 1))  :
    begin
      rw log_div,
      simp only [cast_succ, ne.def, mul_eq_zero, bit0_eq_zero, one_ne_zero, false_or],
      exact cast_ne_zero.mpr (succ_ne_zero n),
      norm_cast,
      exact succ_ne_zero (2*n),
    end
  ... =  log ((2*n.succ)/(2*n + 1))  - log ((2*n)/(2*n + 1)) :
    begin
      rw ‚Üêlog_div,
      simp only [ne.def, mul_eq_zero, bit0_eq_zero, one_ne_zero,
      cast_eq_zero, false_or],
      exact hn,
      norm_cast,
      exact succ_ne_zero (2*n),
    end
  ... = log(((2*n + 1) + 1)/(2*n + 1)) - log ((2*n)/(2*n + 1)) :
  begin
     have h: (2 : ‚Ñù)*n.succ =  2*n + 1 + 1 :=
      begin
      rw succ_eq_add_one,
      norm_cast,
      end,
    rw h,
  end
  ... = log(((2*n + 1) + 1)/(2*n + 1)) - log ((2*n + 1 - 1)/(2*n + 1)) :
    by simp only [add_sub_cancel]
  ... = log (1 + 1 / (2*n + 1)) - log ((2*n + 1 - 1)/(2*n + 1))  : _
  ... = log (1 + 1 / (2*n + 1)) - log (1 - 1/(2*n +1)) : _,
  rw add_div _ (1 : ‚Ñù),
  rw (div_eq_one_iff_eq h‚ÇÅ).mpr h‚ÇÇ,
  rw sub_div _ (1 : ‚Ñù),
  rw (div_eq_one_iff_eq h‚ÇÅ).mpr h‚ÇÇ,
end

lemma power_series_ln (n : ‚Ñï) (hn: 0 < n): has_sum
(Œª (k : ‚Ñï),
(2:‚Ñù) * (1/(2*(k : ‚Ñù) + 1))*((1/(2*(n:‚Ñù) + 1))^(2*k + 1)))
(log (‚Üën.succ / ‚Üën)) :=
 begin
  have h‚ÇÄ: 0 <  (2 * n +1) := by exact succ_pos',
  have h‚ÇÅ: |1 / (2 * (n : ‚Ñù) + 1)| < 1 :=
  begin
    norm_cast,
    rw abs_of_pos,
    rw div_lt_one,
    norm_cast,
    rw add_comm,
    apply lt_add_of_zero_lt_left,
    simp only [canonically_ordered_comm_semiring.mul_pos, succ_pos', true_and, hn],
    norm_cast,
    exact h‚ÇÄ,
    simp only [cast_add, cast_mul, cast_bit0, cast_one, one_div, inv_pos],
    norm_cast,
    exact h‚ÇÄ,
  end,
  rw aux_log,
  exact log_sum_plus_minus (1/(2*(n : ‚Ñù)+1)) h‚ÇÅ,

  exact ne_of_gt hn,
 end

noncomputable def bn (n : ‚Ñï) : ‚Ñù := log (an n)

lemma zero_lt_sqrt_two_n (n : ‚Ñï) : (n ‚â† 0) ‚Üí 0 < real.sqrt (2 * ‚Üën)  :=
begin
  intro hn,
  apply real.sqrt_pos.mpr,
  norm_cast,
  have hn : 0<n, from zero_lt_iff.mpr hn,
  apply mul_pos two_pos ,
  assumption,
  exact nat.nontrivial,
end

lemma n_div_exp1_pow_gt_zero(n : ‚Ñï) :  (‚Üën / exp 1) ^ n >0 :=
begin
  cases n,
  rw pow_zero,
  exact one_pos,
  have hsucc : n.succ > 0, from nat.succ_pos n,
  apply gt_iff_lt.mpr,

  apply pow_pos  _ n.succ,
  apply div_pos_iff.mpr,
  left, split,
  norm_cast, rw ‚Üêgt_iff_lt,
  exact hsucc,
  exact (1:‚Ñù).exp_pos,
end

lemma bn_formula (n : ‚Ñï):  bn n.succ = (log ‚Üën.succ.factorial) -
1/(2:‚Ñù)*(log (2*‚Üën.succ)) - ‚Üën.succ*log (‚Üën.succ/(exp 1)) :=
begin
  have h3, from  (lt_iff_le_and_ne.mp (zero_lt_sqrt_two_n n.succ (succ_ne_zero n))),
  have h4, from  (lt_iff_le_and_ne.mp (n_div_exp1_pow_gt_zero n.succ )),
  rw [bn, an, log_div, log_mul, sqrt_eq_rpow, log_rpow, log_pow],
  ring,
  rw zero_lt_mul_left,
  norm_cast,
  exact succ_pos n,
  exact zero_lt_two,
  exact h3.right.symm,
  exact h4.right.symm,
  norm_cast,
  exact n.succ.factorial_ne_zero,
  apply (mul_ne_zero h3.right.symm h4.right.symm),
end
